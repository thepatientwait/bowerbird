########
# init #
########

import datetime
import os

#############
# functions #
#############

def read_file(file_path):
    if os.path.exists(file_path):
        with open(file_path, 'r') as file:
            return file.read().strip()
    else:
        return None

##########
# config #
##########

os.chdir(workflow.basedir)

configfile: 'config/base.yaml'

if os.path.exists('../../../config/base.yaml'):
    configfile: '../../../config/base.yaml'


#############
# wildcards #
#############

TARGETS                                 = config['TARGETS']

##########
# inputs #
##########

INPUT_TAXONOMY_QUERY                    = 'sql_queries/taxonomy_query.sql'
INPUT_METADATA_QUERY                    = 'sql_queries/metadata_query.sql'



###########
# outputs #
###########

# directories
OUTPUT_DIR                              = 'outputs'
TARGET_DIR                              = os.path.join(OUTPUT_DIR, '{target}_metadata')

# files
OUTPUT_TAXONOMY_TABLE                   = os.path.join(OUTPUT_DIR, 'taxonomy_table.json')

NEW_METADATA_QUERY                      = os.path.join(TARGET_DIR, 'new.json')
OUTPUT_METADATA_QUERY                   = os.path.join(TARGET_DIR, '{new_date}.json')

# log
OUTPUT_DATE                             = os.path.join(TARGET_DIR, 'last_run')


#############
# variables #
#############

OLD_METADATA_DICT = {}

for target in TARGETS:
    if os.path.exists(OUTPUT_DATE.format(target=target)):
        OLD_METADATA_DICT[target]           = read_file(OUTPUT_DATE.format(target=target))
    else:
        OLD_METADATA_DICT[target]           = None

if config['START_DATE'] != 'None':
    OLD_DATE                                = config['START_DATE']
else:
    OLD_DATE                                = str(datetime.date.today())

if config['END_DATE'] != 'None':
    NEW_DATE                                = config['END_DATE']
else:
    NEW_DATE                                = str(datetime.date.today())
# NEW_DATE                                = '2024-09-20'



################
# housekeeping #
################

DIR_NAMES                               = [OUTPUT_DIR] + expand(TARGET_DIR, target=TARGETS)

for dir_name in DIR_NAMES:
    os.makedirs(dir_name, exist_ok=True)

#########
# rules #
#########


rule all:
    input:
        expand(
            OUTPUT_METADATA_QUERY,
            target=TARGETS, new_date=NEW_DATE),
        expand(
            NEW_METADATA_QUERY,
            target=TARGETS),
        expand(
            OUTPUT_DATE,
            target=TARGETS),

rule taxonomy_table:
    input:
        query       = INPUT_TAXONOMY_QUERY
    params:
        new_date    = NEW_DATE,
    output:
        json        = OUTPUT_TAXONOMY_TABLE
    script:
        "scripts/get_query.py"
    

rule metadata_query:
    input:
        taxonomy    = OUTPUT_TAXONOMY_TABLE,
        query       = INPUT_METADATA_QUERY
    params:
        new_date    = NEW_DATE,
        old_date    = OLD_DATE,
        target      = '{target}'
    output:
        json        = NEW_METADATA_QUERY
    script:
        "scripts/get_query.py"


rule merge_metadata:
    input:
        new_samples     = NEW_METADATA_QUERY,
    params:
        old_metadata    = lambda wildcards: OLD_METADATA_DICT[wildcards.target],
    output:
        json            = OUTPUT_METADATA_QUERY
    script:
        "scripts/merge_metadata.py"


rule log_date:
    input:
        expand(
            OUTPUT_METADATA_QUERY,
            new_date=NEW_DATE, allow_missing=True),
    output:
        file        = OUTPUT_DATE
    run:
        with open(output.file, 'w') as file:
            file.write(NEW_DATE)